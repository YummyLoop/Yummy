plugins {
    id "architectury-plugin"
    id "forgified-fabric-loom" apply false
    id "org.jetbrains.kotlin.jvm"
    id "com.github.johnrengelman.shadow"
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "forgified-fabric-loom"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "com.github.johnrengelman.shadow"

    //libsDirName = "../out" // Output location of jar instead of default build/libs

    loom {
        silentMojangMappingsLicense()
        mixinConfigs = ["mixin.examplemod.json"]
    }

    apply from: "${rootDir}/repositories.gradle"
    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        //mappings loom.officialMojangMappings()
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
    }

    // Source files location, currently makes the source location for java and kotlin the same
    sourceSets {
        main.kotlin.srcDirs += 'src/main'
        main.java.srcDirs += 'src/main'
    }
    compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    archivesBaseName = rootProject.archives_base_name
    version = "${rootProject.mod_version}_${rootProject.minecraft_version}"
    group = rootProject.maven_group

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"

        // The Minecraft launcher currently installs Java 8 for users, so your mod probably wants to target Java 8 too
        // JDK 9 introduced a new way of specifying this that will make sure no newer classes or methods are used.
        // We'll use that if it's available, but otherwise we'll use the older option.
        def targetVersion = 8
        if (JavaVersion.current().isJava9Compatible()) {
            options.release = targetVersion
        }
    }

    java {
        withSourcesJar()
    }

    jar {
        manifest {
            attributes([
                    "Title"    : "${rootProject.archives_base_name}",
                    "Version"  : "${rootProject.mod_version}",
                    "Author"   : "${rootProject.mod_author}",
                    "Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
        }
    }
}

task getJars(type: Copy) {
    dependsOn ':fabric:build'
    dependsOn ':forge:build'

    from('fabric/.build/libs') {
        include "*-fabric.jar"
    }
    from('forge/.build/libs') {
        include "*-forge.jar"
    }
    into "out"
}


