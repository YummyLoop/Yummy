plugins {
    id "com.github.johnrengelman.shadow"
}

configurations {
    shadowCommon
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom{
    mixinConfigs = ["mixin.examplemod.json", "mixin.examplemod.fabric.json"]
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "me.shedaniel:architectury-fabric:${rootProject.architectury_version}"
    //modApi "dev.architectury:architectury-fabric:${rootProject.architectury_version}"

    // Extra dependencies
    modImplementation "net.fabricmc:fabric-language-kotlin:${rootProject.fabric_kotlin_version}"
    modImplementation "com.terraformersmc:modmenu:${rootProject.fabric_mod_ModMenu}"
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-api:${rootProject.fabric_mod_REI}") { exclude(module: "config-2") }
    modRuntime("me.shedaniel:RoughlyEnoughItems:${rootProject.fabric_mod_REI}") { exclude(module: "config-2") }
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${rootProject.cloth_config_version}"
    modImplementation "software.bernie.geckolib:geckolib-fabric-${rootProject.fabric_geckolib_version}:dev"

    implementation(project(path: ":common")) { transitive = false }
    developmentFabric(project(path: ":common")) { transitive = false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive = false }
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
    inputs.property "version", project.version
}

shadowJar {
    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier "fabric"
}

jar {
    classifier "dev"
}

java {
    withSourcesJar()
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}
