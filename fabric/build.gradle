plugins {
    id "com.github.johnrengelman.shadow" version "5.0.0"
}

configurations {
    shadow
}

//libsDirName = "../out" // Output location of jar instead of default build/libs

architectury {
    platformSetupLoomIde()
}

repositories {
    maven { url "https://maven.terraformersmc.com/releases" }
    maven { url "https://maven.shedaniel.me/" }
}

dependencies {
    modCompile "net.fabricmc:fabric-loader:${rootProject.fabric_loader_version}"
    modCompile "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_api_version}"
    // Remove the next line if you don't want to depend on the API
    modCompile "me.shedaniel:architectury:${rootProject.architectury_version}:fabric"

    // Extra dependencies
    modImplementation "net.fabricmc:fabric-language-kotlin:${rootProject.fabric_kotlin_version}"
    modImplementation "com.terraformersmc:modmenu:${rootProject.fabric_mod_ModMenu}"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api:${rootProject.fabric_mod_REI}"
    modRuntime "me.shedaniel:RoughlyEnoughItems:${rootProject.fabric_mod_REI}"

    compileOnly(project(path: ":common")) {
        transitive = false
    }
    runtimeOnly(project(path: ":common", configuration: "transformDevelopmentFabric")) {
        transitive = false
    }
    shadow(project(path: ":common", configuration: "transformProductionFabric")) {
        transitive = false
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier "shadow"
}

remapJar {
    dependsOn(shadowJar)
    input.set(shadowJar.archivePath)
    classifier "fabric"
}